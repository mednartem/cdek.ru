buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

ext {
    allureVersion = "2.13.7"
    selenideVersion = "5.17.0"
    junitVersion = "5.7.0"
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {
    testImplementation "org.aspectj:aspectjweaver:1.9.5"
    testImplementation "com.codeborne:selenide:${selenideVersion}"
    testImplementation "io.qameta.allure:allure-selenide:${allureVersion}"
    testImplementation "io.qameta.allure:allure-ee-junit-platform:3.28.2"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "org.aeonbits.owner:owner:1.0.12"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties += System.properties
    systemProperty "junit.jupiter.extensions.autodetection.enabled", "true"

    systemProperties += [
            'junit.jupiter.execution.parallel.enabled'             : true,
            'junit.jupiter.execution.parallel.mode.default'        : 'concurrent',
            'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent',
            'junit.jupiter.execution.parallel.config.strategy'     : 'fixed',
    ]

    def threads = System.getProperty("threads")
    if (threads) {
        systemProperties += [
                'junit.jupiter.execution.parallel.config.fixed.parallelism': threads
        ]
    } else {
        systemProperties += [
                'junit.jupiter.execution.parallel.config.fixed.parallelism': 2
        ]
    }

    testLogging {
        lifecycle {
            events "passed", "failed"
//            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task auth(type: Test) {
    useJUnitPlatform() {
        includeTags 'authorization'
    }
}

task mainPage(type: Test) {
    useJUnitPlatform() {
        includeTags 'mainPage'
    }
}

task calculator(type: Test) {
    useJUnitPlatform() {
        includeTags 'calculator'
    }
}